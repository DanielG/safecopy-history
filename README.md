safecopy-history
================

Automatically keep old versions of datatypes for backwards compatibility and
migration using [`safecopy`](https://github.com/acid-state/safecopy).

Example
-------

Say we have:

```
{-# LANGUAGE TemplateHaskell #-}
import Data.SafeCopy ()
import Data.KeepHistory

type Name     = String
type Address  = String

keepHistory [d|
  data Contacts = Contacts [(Name, Address)]
 |]

main = return ()

```

If we want to add a phone number to each contact we could do the following:


```
{-# LANGUAGE TemplateHaskell #-}
import Data.SafeCopy ()
import Data.KeepHistory

type Name     = String
type Address  = String
type Phone    = String

keepHistory [d|
  data Contact = Contact {
               name    :: Name
             , address :: Address
             , phone   :: Phone
             }
  data Contacts = Contacts [Contact]
 |]

main = return ()
```

Which will result in the compiler yelling at us since we didn't provide a way to
migrate from the old version of the datatype:

```
test/Example.hs:9:1:
    No instance for (safecopy-0.9.0.1:Data.SafeCopy.SafeCopy.Migrate
                       Contacts_v1)
      arising from a use of ‘safecopy-0.9.0.1:Data.SafeCopy.SafeCopy.extension’
    In the expression:
      safecopy-0.9.0.1:Data.SafeCopy.SafeCopy.extension
    In an equation for ‘kind’:
        kind = safecopy-0.9.0.1:Data.SafeCopy.SafeCopy.extension
    In the instance declaration for
      ‘safecopy-0.9.0.1:Data.SafeCopy.SafeCopy.SafeCopy Contacts_v1’
```

So we define the migration from `Contacts_v0` (which is auto-generated by
`keepHistory`).

```
{-# LANGUAGE TemplateHaskell, TypeFamilies #-}
import Data.SafeCopy
import Data.KeepHistory

type Name     = String
type Address  = String
type Phone    = String

keepHistory [d|
  data Contact = Contact {
               name    :: Name
             , address :: Address
             , phone   :: Phone
             }
  data Contacts = Contacts [Contact]
 |]

instance Migrate Contacts where
    type MigrateFrom Contacts = Contacts_v0
    migrate (Contacts_v0 contacts) = Contacts
      [ Contact {
          name    = name
        , address = address
        , phone   = ""
        }
      | (name, address) <- contacts ]

main = return ()
```

The history is stored in a `*.hsitory` file next to the source file and contains
a show'ed representation of the datatype:

```
HistoricDataK {histVer = 0, histRefs = Just [], histData = DataK Contact [] [RecConK Contact [(name,NotStrict,ConT Name),(address,NotStrict,ConT Address),(phone,NotStrict,ConT Phone)]]}
HistoricDataK {histVer = 0, histRefs = Just [], histData = DataK Contacts [] [NormalConK Contacts [(NotStrict,AppT ListT (AppT (AppT (TupleT 2) (ConT Name)) (ConT Address)))]]}
HistoricDataK {histVer = 1, histRefs = Just [(Contact,0)], histData = DataK Contacts [] [NormalConK Contacts [(NotStrict,AppT ListT (ConT Contact))]]}
```
